# User specific aliases
alias ..='cd ..'
alias ...='cd ../..'
alias ....='cd ../../..'
alias .....='cd ../../../..'

alias ls='ls -Fa'
alias la='ls -Falh'
alias cp='cp -iv'
alias rm='rm -i'
alias rr="command rm -r"
alias tree='tree -Fa'
alias diff='diff -bB'
alias sc='screen -D -RR'
alias type='type -a'
alias rol='ruby -pe'
alias jc='journalctl -xe'
alias docker-clean='docker system prune -a --volumes'
alias k='kubectl'

alias dstat-full='dstat --epoch --cpu --load --mem --disk --io --net'
alias dstat-top='dstat --epoch --all --top-cpu-adv --top-io-adv --top-bio-adv --top-mem --top-oom --load'

alias -g L='|& ${PAGER}'
alias -g H='| head'
alias -g T='| tail'
alias -g G='| grep'
alias -g W='| wc'
alias -g S='| sed'
alias -g A='| awk'
alias -g P='| peco --select-1'

# IRC irssi
alias irssi='env TERM=screen-256color irssi'

mc () {
    mkdir -p "$1" && cd "$1" || return
}

ide () {
  idea . &
}

tm () {
  if [[ $# = 0 ]]; then
    tmux attach || tmux new
  else
    ssh -t "$@" "tmux attach || tmux new";
  fi
}

alias gg='' &&  unalias gg
gg () {
  ghq get --look "$1"
}

jj () {
  cd "$(ghq list | peco --query "$1" --select-1 | xargs -I{} ghq list --exact --full-path {} )"
}

da () {
  if ! [ -x "$(command -v docker)" ]; then
    error 'docker is not installed. Visit https://docs.docker.com/docker-for-mac/install/ and install it.'
  fi
  if ! [ -x "$(command -v peco)" ]; then
    error 'peco is not installed. Visit https://github.com/peco/peco#installation and install it.'
  fi
  docker exec -it "$(docker ps --format '{{.ID}}\t{{.Image}}\t{{.Command}}\t{{.RunningFor}}\t{{.Status}}\t{{.Ports}}\t{{.Names}}' | peco --select-1 --query "$1" | awk '{print $1}')" ${2:-sh}
}

# https://github.com/mollifier/anyframe
bindkey '^x^k' anyframe-widget-checkout-git-branch
bindkey '^x^r' anyframe-widget-execute-history
bindkey '^x^j' anyframe-widget-insert-git-branch

# Networks
alias troute='traceroute -P icmp -S'
alias myip='dig +short myip.opendns.com @resolver1.opendns.com'
alias localip='ipconfig getifaddr en1'
alias myips='ifconfig -a'

# alias -s (suffix)
# http://d.hatena.ne.jp/itchyny/20130227/1361933011
alias -s py=python
alias -s rb=ruby
if [ "$(uname)" = "Darwin" ]; then
  alias eog='open -a Preview'
fi
# Open image file by `./test.jpg` from shell
# eog is image viewer of Ubuntu
alias -s {png,jpg,bmp,PNG,JPG,BMP}=eog

if [ "$(uname)" = "Darwin" ]; then
  alias google-chrome='open -a Google\ Chrome'
fi
alias chrome='google-chrome'
# Open html file by `./index.html` from shell
alias -s html=chrome

# atool (aunpack, apack, als, acat, adiff, arepack)
## brew install atool
## aptitude install atool
# Open compressed file by `./test.tar.gz`
alias -s {gz,tgz,zip,lzh,bz2,tbz,Z,tar,arj,xz}=aunpack

# Run c, ccp file without typing compiler command
function runcpp () {
    _TARGET="$1" && g++ -O2 -o "$_TARGET.out" "$_TARGET" && shift && ./"$_TARGET.out" $@
}
# Run like `./test.c -a -b someoption zsh is the supreme shell`
alias -s {c,cpp}=runcpp

# vim:set ft=zsh:
